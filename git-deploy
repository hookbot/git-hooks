#!/usr/bin/env perl

=pod

=head1 NAME

git-deploy - Client for push notification deployment

=head1 DESCRIPTION

git-deploy runs as a git client daemon
to pull the changes from the git server
instantly after a push is triggered.

=head1 SYNOPSIS

  git deploy [ --build='make -C src/.' ] [ --fix-nasty ] [ branch ]

=head2 branch

If a <branch> is specified, then it will update to that branch.
By default, the current branch is used.

=head2 build

The --build argument is any command you want to execute
after any files are pulled or updated from git.
By default, no command is run.

=head2 fix-nasty

The --fix-nasty argument will automatically remove the offending
SSH host entry for the git server from known_hosts. Only use this
flag if you've changed the SSH server key on the git server host.
By default, this option is disabled for better security.

=head1 INSTALL

As super user:

  [root@deploy-host ~]# wget -N -P /usr/bin https://raw.githubusercontent.com/hookbot/git-server/master/git-deploy
  [root@deploy-host ~]# chmod 755 /usr/bin/git-deploy
  [root@deploy-host ~]#

As deploy user:

  [puller@deploy-host projectz]$ git deploy master
  [puller@deploy-host projectz]$ echo '0 * * * * cd ~/projectz && git deploy >/dev/null 2>/dev/null' | crontab -
  [puller@deploy-host projectz]$

=head1 AUTHOR

Rob Brown <bbb@cpan.org>

=head1 COPYRIGHT AND LICENSE

Copyright 2015-2024 by Rob Brown <bbb@cpan.org>

This library is free software; you can redistribute it and/or modify it under the same terms as Perl itself.

=cut

use strict;
use FindBin qw($Script $Bin);
use Getopt::Long qw(GetOptions);

our $VERSION = "0.016";

my @invoked = ($0, @ARGV);
my $build = undef;
my $fix_nasty = undef;
GetOptions
    "build:s" => \$build,
    "fix-nasty" => \$fix_nasty,
    or exec perldoc => $0;

my $branch = shift || eval {
    my $scan = `git branch -a 2>/dev/null`;
    return $1 if $scan =~ m{^\* ([\w/]+)}m;
    if ($scan =~ m{^\* .*detached at (\w+)}m) {
        $scan = `git branch -a --contains $1 2>/dev/null`.$scan;
    }
    return $1 if $scan =~ m{^\s+([\w/]+)}m;
} or die localtime().": [$$] Unable to determine which branch to deploy.\n";

sub rebuild {
    if (defined $build) {
        $0 = "$Script - Running build hook ...";
        system $build;
    }
}

umask 0002;
$0 = "$Script - Initial checkout";
system "git checkout $branch";
sleep 1;
rebuild;
while (1) {
    $0 = "$Script - Waiting for push notification ...";
    my $update = `(git fetch ; git checkout $branch ; git rebase origin/$branch) 2>&1`;
    $0 = "$Script - Scanning updates";
    $update .= `git rebase --abort 2>1` if $update =~ /fix conflicts|git rebase.*--continue|you need to resolve your|stop rebasing/;
    if (-M "$Bin/$Script" < 0) {
        # Myself update detected so need to respawn
        warn localtime().": [$$] Auto-update $Script respawning ...\n";
        sleep 1;
        exec @invoked or die localtime().": [$$] $Bin/$Script: RESPAWN FATAL CRASH\n";
    }
    if ($update =~ /POSSIBLE.*SOMEONE.*DOING.*NASTY/) {
        warn $update;
        if ($fix_nasty && $update =~ /host key for (\S+) has changed and you have requested strict checking/) {
            my $nasty = $1;
            warn "--fix-nasty: $nasty: Clearing known_hosts ...\n";
            if (0 == system "ssh-keygen", "-R", $nasty) {
                require Socket;
                if (my $ip = Socket::inet_ntoa(Socket::inet_aton($nasty))) {
                    $nasty .= ",$ip";
                }
                system "ssh-keyscan -t rsa,dsa $nasty | tee -a ~/.ssh/known_hosts";
            }
        }
        else {
            warn "To force deploy to continue anyway, run this: $Script --fix-nasty\n";
        }
        last;
    }
    if ($update =~ /Your branch.*diverged/) {
        # Rebase can't work if there are local divergents.
        warn $update;
        warn localtime().": [$$] Detected local divergence off [$branch] so doing HARD RESET ...\n";
        system "git", "checkout", $branch;
        system "git", "reset", "--hard", "origin/$branch";
        my $running = `ps fauwwx`;
        last if $running =~ /\Q$Script\E - Waiting/;
        $0 = "$Script - Waiting because of local divergence";
        system "sleep 60";
    }
    elsif ($update =~ /You have unstaged changes/) {
        # Rebase can't work if there are local changes.
        # Make sure there aren't multiple pullers choking on the repo
        warn $update;
        my $running = `ps fauwwx`;
        last if $running =~ /\Q$Script\E - Waiting/;
        my $monkey = "";
        $monkey = ": $1" if $update =~ /^M\s+(\S+)/m;
        $0 = "$Script - Waiting because of local modifications$monkey";
        system "sleep 60";
    }
    elsif ($update =~ m{fatal: Unable to create '(.+?)': File exists.}) {
        # Updates cannot work while lock file exists
        warn $update;
        my $broken_lock = $1;
        $0 = "$Script - Choking Locked: $broken_lock";
        my $running = `ps fauwwx`;
        last if $running =~ /\Q$Script\E - Waiting/;
        if ($running !~ /git rebase/) {
            # No other conflicting git process running
            # So lock file is safe enough to be removed
            unlink $broken_lock;
        }
        else {
            system "sleep 60";
        }
    }
    last if $update !~ /rewinding head to replay|fast-forward|but expected|Unpacking objects|Cannot rebase|ecent commit/;
    if ($update =~ /Current branch.*is up to date/) {
        $0 = "$Script - Waiting because everything is updated ...";
        print "Everything was updated perfectly. Sleeping ...\n";
        system "sleep 60";
    }
    $0 = "$Script - Update complete";
    sleep 1;
    rebuild;
}

rebuild;
