#!/usr/bin/perl

use strict;
use warnings;

my $ssh = $ENV{SSH_CLIENT} or die "Only SSH allowed\n";
my $base = $ENV{GIT_DIR} or die "GIT hook ENV malfunction!\n";
my $acl = {};
my $log = {};
my $conf = `git config --list`;
while ($conf =~ s/^acl.(\w+)=(.*)$//m) {
    my $param = $1;
    my $keys = $2;
    foreach my $key (split /,/, $keys) {
        $acl->{$param}->{$key} = 1;
    }
}
while ($conf =~ s/^log.(\w+)=(.*)$//m) {
    $log->{$1} = $2;
}

$SIG{PIPE} = sub { exit 1; };

my $KEY = $ENV{KEY} || "UNKNOWN";
my $ip = $ssh =~ /^([\d\.:]+) / ? $1 : "UNKNOWN";
if ($acl->{deploy}->{$KEY}) {
    warn localtime().": [$KEY] git-server: [$ip] PULL waiting for notification ...\n";
    my $who = "$KEY\@$ip:$base";
    system "hooks/push-notification", $who;
    die "$0: notification failed ($?)\n" if $?;
}
warn localtime().": [$KEY] git-server: [$ip] RUNNING PULL ...\n";
# If you can write or deploy, then you can also read
my $allowed = $acl->{readers}->{$KEY} || $acl->{writers}->{$KEY} || $acl->{deploy}->{$KEY};
# Check for logfile:
if (my $file = $log->{logfile}) {
    if ($file =~ m{^[^/]}) {
        # Force start with a / for absolute path
        $file = "$base/$file";
    }
    if ($file =~ m{^(.*)/[^/]+$}) {
        my $log_dir = $1;
        system mkdir => -p => $log_dir if !-d $log_dir;
        if (open my $fh, ">>", $file) {
            if ($allowed) {
                print $fh localtime().": [$ip] $KEY: pulled\n";
            }
            else {
                print $fh localtime().": [$ip] $KEY: pull attempt blocked!\n";
            }
        }
    }
}
die "$KEY: You have been banned from all read operations!\n" unless $allowed;
